/* hydrogen2.mac */

l: 1; m: 0; k: 4;
alpha: e0*e0/(4*%pi*eps*hb*c); /* fine structure constant */
vl: (sum(a[j]*r^j,j,1,k));
vl: vl+1;
/* yl is the trial function of the radial wave function */
yl: r^(l+1)*exp(-sqrt(g2)*r)*vl;
/* insert ansatz into differential equation */
left: diff(yl,r,2)+(-g2+2*m0*c*alpha/(hb*r)-l*(l+1)/(r*r))*yl;
left: left*exp(sqrt(g2)*r);
left: left/r;
left: ratsimp(left);
left: num(left);
left: expand(left);
left: subst(x,sqrt(g2),left);
h[0]: coeff(left,r,0); 
h[1]: coeff(left,r,1);
h[2]: coeff(left,r,2); 
h[3]: coeff(left,r,3);
h[4]: coeff(left,r,4);
s: solve([h[0]=0,h[1]=0,h[2]=0,h[3]=0,h[4]=0],[x,a[1],a[2],a[3],a[4]]);
sol: last(s);
sol: first(sol);
sol: rhs(sol);
E41: -hb^2*sol*sol/2/m0; /* energy eigenvalue for k=4, l=1 */
print("E41=",E41);
/* The output is the energy eigenvalue E41. */
